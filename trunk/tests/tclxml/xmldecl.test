# Features covered:  XML Declaration
#
# This file contains a collection of tests for the TclXML parser.
# This file tests the parser's performance on XML Declarations.
# Sourcing this file into Tcl runs the tests and generates output
# for errors.  No output means no errors were found.
#
# Copyright (c) 2001-2003 Zveno Pty Ltd.
#
# $Id: xmldecl.test,v 1.3 2003/12/03 20:06:37 balls Exp $

package require tcltest
source [file join [tcltest::workingDirectory] tclxmlutils.tcl]

testPackage $::tcltest::parser

namespace eval ::xml::xmldeclTest {
    namespace import -force ::tcltest::*

    variable SETUP {
	variable result {}

	proc xmldecl {version encoding standalone} {
	    variable result
	    lappend result $version $encoding $standalone
	}

	set parser [xml::parser \
		-xmldeclcommand [namespace code xmldecl]]
    }
    variable CLEANUP {
	catch {unset result}
	rename xmldecl {}
	$parser free
    }

    test xmldecl-1.1 {No XML Declaration} -setup $SETUP -body {
	$parser parse {<Test/>}
	set result
    } -cleanup $CLEANUP -result {}

    test xmldecl-1.2 {Simple XML Declaration} -setup $SETUP -constraints {!xml_libxml2} -body {
	$parser parse {<?xml version='1.0'?>
<Test/>
}

	set result
    } -cleanup $CLEANUP -result [list 1.0 {} {}]

    test xmldecl-1.3 {XML Declaration w/- encoding} -setup $SETUP -constraints {!xml_libxml2} -body {
	$parser parse {<?xml version="1.0" encoding='utf-8'?>
<Test/>
}

	set result
    } -cleanup $CLEANUP -result [list 1.0 utf-8 {}]

    test xmldecl-1.4 {XML Declaration w/- standalone} -setup $SETUP -constraints {!xml_libxml2} -body {
	$parser parse {<?xml version="1.0" standalone='yes'?>
<Test/>
}

	set result
    } -cleanup $CLEANUP -result [list 1.0 {} yes]

    test xmldecl-1.5 {XML Declaration w/- the lot} -setup $SETUP -constraints {!xml_libxml2} -body {
	$parser parse {<?xml version='1.0' encoding="utf-8" standalone='yes'?>
<Test/>
}

	set result
    } -cleanup $CLEANUP -result [list 1.0 utf-8 yes]

    # Test case from bug #434304 submitted by Cameron Laird

    test xmldecl-2.1 {XML Declaration w/- whitespace} -setup $SETUP -constraints {!xml_libxml2} -body {
	$parser parse {<?xml version = '1.0'?>
<Test/>
}

	set result
    } -cleanup $CLEANUP -result [list 1.0 {} {}]

    test xmldecl-3.1 {XML Declaration w/- error} -setup $SETUP -match glob -body {
	expectError {
	    $parser parse {<?xml version = "1.0" foobar standalone='no'?>
<Test/>
}
	}
    } -cleanup $CLEANUP -result *

    cleanupTests
}

namespace delete ::xml::xmldeclTest
return
